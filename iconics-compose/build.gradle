/*
 * Copyright (c) 2020 Mike Penz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.compose"
apply plugin: "org.jetbrains.kotlin.plugin.compose"
apply from: "../gradle/compile.groovy"

version release.versionName

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools
    namespace 'com.mikepenz.iconics.compose'

    defaultConfig {
        minSdkVersion setup.composeMinSdk
        targetSdkVersion setup.targetSdk
        consumerProguardFiles 'consumer-proguard-rules.pro'
        versionCode release.versionCode
        versionName release.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        // specify the artifactId as module-name for kotlin
        freeCompilerArgs += ["-module-name", POM_ARTIFACT_ID]

        if (project.findProperty("composeCompilerReports") == "true") {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                            project.buildDir.absolutePath + "/compose_compiler"
            ]
        }
        if (project.findProperty("composeCompilerMetrics") == "true") {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                            project.buildDir.absolutePath + "/compose_compiler"
            ]
        }
    }

}
if (project.hasProperty('pushall') || project.hasProperty('librarycomposeonly')) {
    apply from: '../gradle-release.gradle'
}

dependencies {
    api project(':iconics-typeface-api')

    implementation(compose.ui)
    implementation(compose.foundation)
}
