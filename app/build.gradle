apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

/*
 * Copyright (c) 2020 Mike Penz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (getSigningFile() != null) {
    apply from: "${getSigningFile()}"
}

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        multiDexEnabled true

        setProperty("archivesBaseName", "Android Iconics-v$versionName-c$versionCode")
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
            if (getSigningFile() != null) {
                signingConfig signingConfigs.debug
            }
            minifyEnabled false
        }
        release {
            if (getSigningFile() != null) {
                signingConfig signingConfigs.release
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
        exclude 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.cardview:cardview:${versions.cardview}"

    implementation "androidx.core:core-ktx:${versions.ktx.core}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    kapt "com.android.databinding:compiler:3.5.3"

    // used to fill the RecyclerView with the DrawerItems
    // and provides single and multi selection, expandable items
    // https://github.com/mikepenz/FastAdapter
    implementation "com.mikepenz:fastadapter:${versions.fastAdapter}"
    implementation "com.mikepenz:fastadapter-extensions-utils:${versions.fastAdapter}"
    implementation "com.mikepenz:fastadapter-extensions-ui:${versions.fastAdapter}"
    implementation "com.mikepenz:fastadapter-extensions-expandable:${versions.fastAdapter}"

    // used to generating string fields for icons (sample - test/java/StringFieldGenerator.java)
    // https://github.com/zTrap/Android-Iconics-String-Generator
    testImplementation 'ru.ztrap.iconics:string-generator:1.0.4'

    // used to generate the drawer on the left
    // https://github.com/mikepenz/MaterialDrawer
    implementation("com.mikepenz:materialdrawer:${versions.materialDrawer}") {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "fastadapter-extensions-expandable"
    }

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    implementation("com.mikepenz:aboutlibraries:${versions.aboutLibraries}") {
        transitive = true
        exclude module: "fastadapter"
    }

    implementation project(':library-core')
    implementation project(':iconics-view-library')

    implementation project(':community-material-typeface-library')
    implementation project(':devicon-typeface-library')
    implementation project(':entypo-typeface-library')
    implementation project(':fontawesome-typeface-library')
    implementation project(':foundation-icons-typeface-library')
    implementation project(':google-material-typeface-library')
    implementation project(':ionicons-typeface-library')
    implementation project(':material-design-dx-typeface-library')
    implementation project(':material-design-iconic-typeface-library')
    implementation project(':meteocons-typeface-library')
    implementation project(':octicons-typeface-library')
    implementation project(':phosphor-typeface-library')
    implementation project(':pixeden-7-stroke-typeface-library')
    implementation project(':typeicons-typeface-library')
    implementation project(':weather-icons-typeface-library')

    testImplementation 'junit:junit:4.13'
}
