apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 28011
        versionName "2.8.1"
        enforceUniquePackageName false
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = outputFile.name.replace(".apk", "-v" + defaultConfig.versionName + "-c" + defaultConfig.versionCode + ".apk")
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
    productFlavors {
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"

    // used to base on some backwards compatible themes
    // contains util classes to support various android versions, and clean up code
    // comes with the awesome "Holder"-Pattern
    // https://github.com/mikepenz/Materialize
    compile 'com.mikepenz:materialize:1.0.0@aar'

    // used to fill the RecyclerView with the DrawerItems
    // and provides single and multi selection, expandable items
    // https://github.com/mikepenz/FastAdapter
    compile 'com.mikepenz:fastadapter:1.8.0@aar'

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:5.6.0@aar') {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "iconics-core"
    }

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    compile('com.mikepenz:aboutlibraries:5.8.1@aar') {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "iconics-core"
    }

    compile project(':fontawesome-typeface-library')
    compile project(':material-design-iconic-typeface-library')
    compile project(':google-material-typeface-library')
    compile project(':meteocons-typeface-library')
    compile project(':weather-icons-typeface-library')
    compile project(':octicons-typeface-library')
    compile project(':community-material-typeface-library')
    compile project(':entypo-typeface-library')
    compile project(':typeicons-typeface-library')
    compile project(':devicon-typeface-library')
    compile project(':foundation-icons-typeface-library')
    compile project(':ionicons-typeface-library')
}
